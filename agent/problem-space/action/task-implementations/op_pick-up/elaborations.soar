sp {op_pick-up1*elaborate*target-object*superoperator
   (state <s> ^name op_pick-up1
              ^task-operator.arg1.id <obj>)
-->
   (<s> ^target-object <obj>)
}

### For the object to be accessible, it must not be inside a closed receptacle

# receptacle the object is inside
sp {op_pick-up1*elaborate*receptacle
   (state <s> ^name op_pick-up1
              ^target-object <obj>
              ^world.predicates.predicate <in-pred>)
   (<in-pred> ^handle in1
              ^instance <i>)
   (<i> ^1 <obj> ^2 <rec>)
   (<rec> ^predicates.affordance1 receptacle1)
-->
   (<s> ^receptacle <rec>)
}

# accessible if the receptacle is open
sp {op_pick-up1*elaborate*accessible*true*open
   (state <s> ^name op_pick-up1
              ^receptacle.predicates.<prop> open2)
-->
   (<s> ^accessible true)
}

# accessible if the receptacle has no door (neither open or closed)
sp {op_pick-up1*elaborate*accessible*true*no-door
   (state <s> ^name op_pick-up1
              ^receptacle.predicates <preds>)
   (<preds> -^<prop> << open2 closed2 >>)
-->
   (<s> ^accessible true)
}

# accessible if not inside a receptacle
sp {op_pick-up1*elaborate*accessible*true*no*receptacle
   (state <s> ^name op_pick-up1
             -^receptacle)
-->
   (<s> ^accessible true)
}

