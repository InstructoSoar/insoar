
#create model template
sp {learn-cause*elaborate*query-smem-for-model*attribute
(state <s> ^name learn-causal-model)
-->
(<s> ^model-template.model <mdtmp>)
(<mdtmp> ^model-name learned-causal-model)
}

#fill in participants
sp {learn-cause*propose*learn-participants
(state <s> ^name learn-causal-model
		   ^model-template.model <mdtmp>)
(<mdtmp> -^participants <par>)
-->
(<s> ^operator <o> + =)
(<o> ^name learn-participants)

}

sp {learn-cause*apply*learn-participants
(state <s> ^name learn-causal-model
		   ^model-template.model <mdtmp>
		   ^operator.name learn-participants
		   ^superstate <s1>)
(<s1> ^purpose.parameters <p>)
(<p> ^participant1.root-category <cat1>
	^participant2.root-category <cat2>)
-->
(<mdtmp> ^participants <partic>)
(<partic> ^participant <par1>
		  ^participant <par2>)
(<par1> ^category <cat1>)
(<par2> ^category <cat2>)
}

# #fill in cause
sp {learn-cause*propose*learn-cause
(state <s> ^name learn-causal-model
		   ^model-template.model <mdtmp>)
(<mdtmp> ^participants <par>
	     -^causes <cs>)
-->
(<s> ^operator <o> + =)
(<o> ^name learn-cause)
}

sp {learn-cause*apply*learn-cause
(state <s> ^name learn-causal-model
		   ^model-template.model <mdtmp>
		   ^operator.name learn-cause
		   ^superstate <s1>)
(<mdtmp> ^participants.participant <par1>)
(<par1> ^category <rootcat>)
(<s1> ^purpose.parameters.cause <css>)
(<css> ^action <ac>
	  ^arg1.2 <obj>)
(<obj> ^root-category <rootcat>)
(<ac> ^handle <tskhandle>
	  ^procedural.op_name <opname>)
-->
(<mdtmp> ^causes.cause <cs>)
(<cs> ^task-operator <tsk>
	  ^object <obj1>)
(<obj1> ^participant <par1>)
(<tsk>	^name <opname>
		^item-type task-operator
		^task-handle <tskhandle>
		^perform multiple
		^arg1 <2arg1>)
(<2arg1> ^arg-type object
		^temp-id <par1>)
}

#fill in effects
sp {learn-cause*propose*learn-effect
(state <s> ^name learn-causal-model
		   ^model-template.model <mdtmp>)
(<mdtmp> ^participants <par>
	     ^causes <cs>
	     -^effects)
-->
(<s> ^operator <o> + =)
(<o> ^name learn-effect)
}

sp {learn-cause*apply*learn-effect
(state <s> ^name learn-causal-model
		   ^model-template <mdtmp>
		   ^operator.name learn-effect
		   ^superstate <s1>)
(<mdtmp> ^model <mod>)
(<mod> ^participants.participant <par1>)
(<par1> ^category <rootcat>)
(<s1> ^purpose.parameters.effect <eff>)
(<eff> ^arg.root-category <rootcat>
 	   ^pred-change <predch>)
(<predch> ^name <predname>
		  ^value <predvalue>)
-->
(<mod> ^effects.effect <ef>)
(<ef> ^model-reference <mod>
 	  ^participant-category <rootcat>
 	  ^participant <par1>
	  ^pred-name <predname>
	  ^pred-value <predvalue>)	
}


#fill in precondtion















