
#queries for the effect (one object with a predicate)
sp {retrieve-model*elaborate*query-smem-for-model*attribute
(state <s> ^name retrieve-model
		^name task-state-no-change
		^desired <des>
		^model <md>)
(<md> -^retrieved <x>)
(<des> ^1 <goal>)
(<goal> ^1.root-category <cat>
		^property-handle <predName>
		^handle <predValue>)
-->
(<s> ^smem-query <query>)
(<query> ^cue <cue>
		 ^destination <s>
		 ^result-name smem-model
		 ^depth 10)
(<cue> ^pred-name <predName>
	   ^pred-value <predValue>
	   ^participant-category <cat>)
}

#queries for the effect (two objects with a relation)
sp {retrieve-model*elaborate*query-smem-for-model*relation
(state <s> ^name retrieve-model
		^name task-state-no-change
		^desired.1 <des>
		^model <md>)
(<md> -^retrieved <x>)
(<des> ^1 <ob1>
	   ^2 <ob2>
	   ^handle <predName>
	   ^type relation)
(<ob1> ^category <rootcat1>)
(<ob2> ^category <rootcat2>)
-->
(<s> ^smem-query <query>)
(<query> ^cue <cue>
		 ^destination <s>
		 ^result-name smem-model
		 ^depth 10)
(<cue> ^participant <par1>
	   ^participant <par2>
	   ^pred-name <predName>
	   ^pred-value <par1>
	   ^pred-value <par2>
	   ^participant-category <rootcat1>
	   ^participant-category <rootcat2>)
}


sp {retrieve-model*elaborate*create*retrieved
(state <s> ^name retrieve-model
		^name task-state-no-change
		^model <md>
		^smem-model <y>)
(<y> ^model-reference <ref>)
(<ref> ^model-name <name>)
-->
(<md> ^retrieved <ref>)
(write |Retrieved model | <name> |.| )

}

sp {retrieve-model*propose*model-smem-failure
(state <s> ^name retrieve-model
		^name task-state-no-change
		^smem-model failure)
-->
(<s> ^operator <o> +)
(<o> ^name model-smem-failure)
}

sp {retrieve-model*apply*model-failure
(state <s> ^name retrieve-model
		^name task-state-no-change
		^operator <o>)
(<o> ^name model-smem-failure)
-->
(<s> ^retrieved-model failure)
}
