sp {goal-learning*defintion*elaborate*from*superoperator
   (state <s> ^name definition
              ^superstate.operator <ssop>)
   (<ssop> ^indexed-action <ia>
           ^goal-description <goal>)
-->
   (<s> ^indexed-action <ia>
        ^goal <goal>)
}

sp {goal-learning*definition*elaborate*goal-id
   (state <s> ^name definition
              ^indexed-action._metadata.map-id.goal <gid>)
-->
   (<s> ^goal-id <gid>)
}

sp {action-definition*elaborate*goal-id*relation-predicate
   (state <s> ^name definition
   	      ^goal.{<pred> << relation-predicate state-predicate >>} <rel>
	      ^indexed-action._metadata.map-id.goal <gid>
	      ^smem.command <scomm>)
-->
   (<gid> ^<pred> <nrel>)
   (<nrel> ^identifier (make-constant-symbol))
   (<s> ^pairing <p>)
   (<p> ^smem <nrel>
   	^wmem <rel>)
   (<scomm> ^store <nrel> <gid>)
}

### elaborate objects that can be generalized away
sp {action-goal-learning*objects*generalizable*level1
   (state <s> ^name definition
   	      ^indexed-action <ia>
	      ^goal <goal>
	      ^smem.command <scomm>)
   (<ia> ^_metadata.map-id <map>)
   (<map> ^procedural <proc-id>
   	  ^goal <gid>)
   (<goal> ^<< relation-predicate state-predicate >> <relp>)
   (<ia> ^<argument> <obj>)
   (<relp> ^<relarg> <obj>)
   (<proc-id> ^<argument> <sobj>)
-->
   (<s> ^generalizable <gen>)
   (<gen> ^wme-id <obj>
   	  ^lt-id <sobj>)
}

sp {action-goal-learning*objects*generalizable*level2*argument*object
   (state <s> ^name definition
   	      ^indexed-action <ia>
	      ^goal <goal>
	      ^smem.command <scomm>)
   (<ia> ^_metadata.map-id <map>)
   (<map> ^procedural <proc-id>
   	  ^goal <gid>)
   (<goal> ^<< relation-predicate state-predicate >> <relp>)
   (<ia> ^<argument> <arg-obj>)
   (<arg-obj> ^argument <obj>)
   (<relp> ^<relarg> <obj>)
   (<proc-id> ^<argument> <sarg-obj>)
   (<sarg-obj> ^argument <sobj>)
-->
   (<s> ^generalizable <gen>)
   (<gen> ^wme-id <obj>
   	  ^lt-id <sobj>)
}

sp {action-goal-learning*objects*generalizable*level2*relation
   (state <s> ^name definition
   	      ^indexed-action <ia>
	      ^goal <goal>
	      ^smem.command <scomm>)
   (<ia> ^_metadata.map-id <map>)
   (<map> ^procedural <proc-id>
   	  ^goal <gid>)
   (<goal> ^<< relation-predicate state-predicate >> <relp>)
   (<ia> ^<argument> <arg-obj>)
   (<arg-obj> ^relation.smem-rep <obj>)
   (<relp> ^relation <xrel>)
   (<xrel> ^smem-rep <obj>)
   (<proc-id> ^<argument> <sarg-obj>)
   (<sarg-obj> ^relation <sobj>)
-->
   (<s> ^generalizable <gen>)
   (<gen> ^wme-id <xrel>
   	  ^lt-id <sobj>)
}