sp {infer-goal*propose*generate-goal-description
   (state <s> ^name infer-goal
              ^diff-result <res>)
-->
   (<s> ^operator <o> +)
   (<o> ^name generate-goal-description
        ^diff <res>)
}

sp {infer-goal*apply*generate-goal-description*create*goal-description
   (state <s> ^name infer-goal
              ^operator.name generate-goal-description)
-->
   (<s> ^goal-description <desc>)
}

sp {infer-goal*apply*generate-goal-description*create*state-predicate
   (state <s> ^name infer-goal
              ^operator <o>
              ^goal-description <desc>
              ^used-objects.object <obj>) 
   (<o> ^name generate-goal-description 
        ^diff.predicates.predicate <pred>)
   (<pred> ^object <obj>
           ^final-value <pred-val>)
-->
   (<desc> ^state-predicate <state-pred>)
   (<state-pred> ^state <pred-val>
                 ^p1 <obj>)
}


sp {infer-goal*apply*generate-goal-description*create*relation-predicate
   (state <s> ^name infer-goal
              ^operator <o>
              ^goal-description <desc>
              ^used-objects.object <obja>
              ^used-objects.object <objb>
              ^used-relations.relation.type <rel-name>
              ^topstate <top>)
   (<o> ^name generate-goal-description
        ^diff.relationships.relations <rels>)
   (<rels> ^name <rel-name>
           ^pair <pair>)
   (<pair> ^a <ida> ^b <idb>)
   (<top> ^object <obja>
          ^object <objb>
          ^found-prep <prep>)
   (<obja> ^id <ida>)
   (<objb> ^id <idb>)
   (<prep> ^type <rel-name>)
-->
   (<desc> ^relation-predicate <rel-pred>)
   (<rel-pred> ^p1 <obja>
               ^p2 <objb>
               ^relation <prep>)
}

sp {infer-goal*apply*generate-goal-description*mark*copied
   (state <s> ^name infer-goal
              ^operator.name generate-goal-description
              ^goal-description <desc>)
-->
   (<s> ^copied true)
}

sp {infer-goal*apply*generate-goal-description*copy*goal-description
   (state <s> ^name infer-goal
              ^operator.name generate-goal-description
              ^goal-description <desc>
              ^copied true
              ^superstate <ss>)
-->
   (<ss> ^goal-description <desc>)
}
   