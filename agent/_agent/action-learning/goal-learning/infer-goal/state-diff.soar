sp {infer-goal*propose*state-diff
   (state <s> ^name infer-goal
              ^unchanged-state <unchanged>
              ^final-state <final>
             -^diff-result)
-->
   (<s> ^operator <o> +)
   (<o> ^name state-diff
        ^initial <unchanged>
        ^final <final>
        ^attend-only <only>)
   (<only> ^objects <objs>
           ^relations <rels>
           ^predicates <preds>)
}

sp {infer-goal*elaborate*state-diff*attend*objects
   (state <s> ^name infer-goal
              ^operator <o> +
              ^used-objects.object <obj>)
   (<o> ^name state-diff
        ^attend-only.objects <objs>)
-->
   (<objs> ^object <obj>)
}

sp {infer-goal*elaborate*state-diff*attend-only*relations
   (state <s> ^name infer-goal
              ^operator <o> +
              ^used-relations.relation.type <rel-name>)
   (<o> ^name state-diff
        ^attend-only.relations <rels>)
-->
   (<rels> ^relation <rel-name>)
}

sp {infer-goal*elaborate*state-diff*attend-only*predicates
   (state <s> ^name infer-goal
              ^operator <o> +
              ^used-predicates.predicate <pred-name>)
   (<o> ^name state-diff
        ^attend-only.predicates <preds>)
-->
   (<preds> ^predicate <pred-name>)
}