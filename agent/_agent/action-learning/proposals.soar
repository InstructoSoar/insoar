### propose goal-definition learning
sp {learning*propose*goal-learning
   (state <s> ^name action-learning
   	      ^purpose <p>
          ^topstate.interaction.stack.segment.next.purpose <prev>)
   (<p> ^type goal-description
        ^parameters.indexable.lexical <lex>
        ^parameters.indexable.result <goal>)
   (<prev> ^type action-command
           ^parameters.indexable.result <ia>)
   
-->
   (<s> ^operator <op> +)
   (<op> ^name goal-learning
         ^goal-description <goal>
         ^indexed-action <ia>
         ^goal-lexical <lex>)
}

sp {action-learning*propose*goal-learning*inferred
   (state <s> ^name action-learning
              ^purpose <p>
              ^goal-description <goal>
             -^learned-goal <goal>)
   (<p> ^type action-command
        ^parameters.indexable.result <ia>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name goal-learning
        ^goal-description <goal>
        ^indexed-action <ia>)
}

### retrospective learning for learning polity by instruction exploitation, proposal conditions
sp {learning*propose*retrospective-learning
   (state <s> ^name action-learning
   	      ^purpose <p>
	      ^topstate.interaction.stack.segment <seg>)
   (<p> ^type action-command)
-->
   (<s> ^operator <op> +)
   (<op> ^name retrospective-learning
   	 ^purpose <p>
	 ^segment <seg>)
}

# IROS: propose infer-goal when there is no goal before a retrospective analysis
sp {learning*propose*infer-goal*no*goal
   (state <s> ^name action-learning
              ^purpose <p>
              ^topstate.interaction.stack.segment.purpose.parameters.indexable.result <ia>
             -^goal-description)
   (<p> ^type action-command)
   (<ia> ^_metadata.map-id.goal <gid>)
   (<gid> -^<pred>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name infer-goal)
}
