#copy relationships that are new (in final state and not initial)
#ignore relationships that are in ignore list                                
sp {state-union*propose*copy-relationships
   (state <s> ^name state-union
             -^relationships.relations)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-relationships)
}

sp {state-union*apply*copy-relationships
   (state <s> ^name state-union
              ^relationships <r>
             -^ignore.relation <name>
              ^operator.name copy-relationships
              ^initial.relationships.relations <rels>
              ^final.relationships.relations <rel2>)
   (<rel2> ^name <name> ^pair <pair>)
   (<pair> ^a <ida> ^b <idb> -^c)
   (<rels> ^name <name> ^pair <pair2>)
   (<pair2> ^a <ida> ^b <idb> -^c)
 -{(<s> ^ignore.object.id <id>)
     (<pair> ^<< a b c >> <id>)}
-->
   (<r> ^relations <r1>)
   (<r1> ^name <name> ^pair <pair>)
}
sp {state-union*apply*copy-relationships*3-args
   (state <s> ^name state-union
              ^relationships <r>
             -^ignore.relation <name>
              ^operator.name copy-relationships
              ^initial.relationships.relations <rels>
              ^final.relationships.relations <rel2>)
   (<rel2> ^name <name> ^pair <pair>)
   (<pair> ^a <ida> ^b <idb> ^c <idc>)
   (<rels> ^name <name> ^pair <pair2>)
   (<pair2> ^a <ida> ^b <idb> ^c <idc>)
 -{(<s> ^ignore.object.id <id>)
   (<pair> ^<< a b c >> <id>)}
-->
   (<r> ^relations <r1>)
   (<r1> ^name <name> ^pair <pair>)
}

#copy objects that appear in the new relationships
#ignore objects from ignore list
sp {state-union*propose*copy-objects
   (state <s> ^name state-union
             -^object-set.object)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-objects)
}
sp {state-union*apply*copy-objects
   (state <s> ^name state-union
              ^relationships.relations <p>
              ^object-set <os>
             -^ignore.object <obj>
              ^object-desc-set <ods>
              ^initial.objects.object <obj>
              ^final.object-desc-set.obj-desc <desc>
              ^final.objects.object <obj>
              ^operator.name copy-objects
              ^relationships <rels>)
   (<p> ^name <name> ^pair <pair>)
   (<pair> ^<< a b c >> <id>)
   (<obj> ^id <id>)
   (<desc> ^id <id>)
-->
   (<os> ^object <obj>)
   (<ods> ^obj-desc <desc>)
}


#complete, copy to output state-result
sp {state-union*propose*complete
   (state <s> ^name state-union)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete)
}

sp {state-union*apply*complete
   (state <s> ^name state-union
              ^object-set <os>
              ^relationships <rels>
              ^object-desc-set <ods>
              ^superstate.operator <op>
              ^operator.name complete)
-->
   (interrupt)
   (<op> ^state-result <res>)
   (<res> ^relationships <rels> ^objects <os> ^object-desc-set <ods>)
}