sp {format-conditions*elaborate*parameter-set
   (state <s> ^name format-conditions)
-->
   (<s> ^parameter-set <ps>
        ^a 1 ^b 2 ^c 3)
}
sp {format-conditions*elaborate*operator*condition
   (state <s> ^name format-conditions
              ^superstate.operator.nlp-set.nlp <nlp>)
-->
   (<s> ^condition <nlp>
        ^condition-set <nlp>)
}

sp {format-conditions*elaborate*sub-conditions
   (state <s> ^name format-conditions
              ^condition <co>)
   (<co> ^args.{<arg> << 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 >>} <cond>)
   (<cond> ^name <name>)
-->
   (<s> ^condition <cond>)
}

sp {format-conditions*propose*calculate1
   (state <s> ^name format-conditions
             -^tested calculate1)
-->
   (<s> ^operator <op> + =)
   (<op> ^name calculate1)
}
sp {format-conditions*propose*calculate2
   (state <s> ^name format-conditions
              ^tested calculate1
             -^tested calculate2)
-->
   (<s> ^operator <op> + =)
   (<op> ^name calculate2)
}
sp {format-conditions*propose*complete
   (state <s> ^name format-conditions
              ^tested calculate1
              ^tested calculate2)
-->
   (<s> ^operator <op> + =)
   (<op> ^name complete)
}

sp {format-conditions*apply*complete*parameter-set
   (state <s> ^name format-conditions
              ^parameter-set <ps>
              ^superstate <ss>
              ^operator.name complete)
   (<ss> ^<type> <t> ^operator.type <type>)
   (<t> ^parameter-set <ps2>)
   (<ps> ^argnum <num>)
  # (<cs> ^parameter.result.link.link.<s2>.<i2>.<elem> holder)
-->
   (<ps2> ^argnum <num>)
   (<t> ^formated <type>)
}
sp {format-conditions*apply*complete*condition
   (state <s> ^name format-conditions
              ^parameter-set <ps>
              ^condition-set <cs>
              ^superstate <ss>
              ^operator.name complete)
   (<ss> ^<type> <t> ^operator.type <type>)
  # (<cs> ^parameter.result.link.link.<s2>.<i2>.<elem> holder)
-->
   (<t> ^condition <cs>)
}

sp {format-conditions*apply*calculate1*default
   (state <s> ^name format-conditions
              ^operator.name calculate1)
  -{(<s> ^condition <c>)
     (<c> -^rtype)}
-->
   (<s> ^tested calculate1)
}
sp {format-conditions*apply*calculate2*default
   (state <s> ^name format-conditions
              ^operator.name calculate2)
  -{(<s> ^condition.parameter.result.link <l>)
     (<l> -^link)}
 -{(<s> ^condition <c>)
     (<c> -^result)}
-->
   (<s> ^tested calculate2)
}
sp {format-conditions*apply*calculate1*negative-test
   (state <s> ^name format-conditions
              ^operator.name calculate1
              ^condition <co>)
   (<co> -^negative)
-->
   (<co> ^negative false)
}
sp {format-conditions*apply*calculate1*no-object
   (state <s> ^name format-conditions
              ^operator.name calculate1
             -^condition.name object)
-->
   (<s> ^condition <c2>)
   (<c2> ^name object ^args <a3> ^negative false ^result.set.item <res> ^parameter.result.link.link.set.item.object holder ^rtype single)
}

sp {format-conditions*apply*calculate1*rtype
   (state <s> ^name format-conditions
              ^operator.name << calculate1 calculate2 >>
              ^condition <co>)
   (<co> -^rtype ^parameter.result.link.type <type>)
-->
   (<co> ^rtype <type>)
}
sp {format-conditions*apply*calculate1*rtype*no-type
   (state <s> ^name format-conditions
              ^operator.name << calculate1 calculate2 >>
              ^condition <co>)
   (<co> ^args.{<arg> 1}.rtype <type> -^rtype -^parameter) #-^parameter .result.link.type
-->
   (<co> ^rtype <type>)
}
sp {format-conditions*apply*calculate1*type
   (state <s> ^name format-conditions
              ^operator.name << calculate1 calculate2 >>
              ^condition <co>)
   (<co> ^parameter.{<arg> << 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 >>} <a1> ^args.<arg>.rtype <type>)
   (<a1> -^type)
-->
   (<a1> ^type <type>)
}

sp {format-conditions*apply*calculate1*empty-args
   (state <s> ^name format-conditions
              ^operator.name calculate1
              ^condition <co>)
   (<co> -^args)
-->
   (<co> ^args <a1>)
}

#-^parameter and ^rtype single->^parameter.result.link.link.set.item.object holder default
sp {format-conditions*apply*calculate1*empty-parameter
   (state <s> ^name format-conditions
              ^operator.name calculate2
              ^condition <co>)
   (<co> -^parameter)
-->
   (<co> ^parameter.result.link.link.set.item.object holder)
}


sp {format-conditions*apply*calculate2*paramlinking
   (state <s> ^name format-conditions
              ^operator.name calculate2
              ^condition <co>)
   (<co> ^parameter.result.link.link.<s2>.<i2>.<elem> holder -^result)
-->
   (<co> ^result.<s2>.<i2> <res>)
}

sp {format-conditions*apply*calculate1*location-block-noobj
   (state <s> ^name format-conditions
              ^operator.name calculate2
              ^condition <c2>
              ^condition <co>)
   (<co> ^name << location block >> ^rtype <type> ^args <a1>)
   (<a1> -^<something>)
   (<c2> ^name object)
 # -{(<s> ^condition {<c3> > <c2>}) #to limit to one object if multiple
 #   (<c3> ^name object ^rtype <type>)}
-->
   (<a1> ^num 1 ^1 <c2>)
}

#parameter ^num <arg> is highest of parameter.<arg>
#elab parameter-set.argnum max param <id> num
sp {format-conditions*apply*calculate1*parameter-num
   (state <s> ^name format-conditions
              ^operator.name calculate1
              ^condition <co>)
   (<co> ^parameter <p>)
   (<p> -^num ^{<arg> << 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 >>} <p1>)
   (<p> -^{<arg2> << 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 >> > <arg>} <p2>)
-->
   (<p> ^num <arg>)
}
sp {format-conditions*apply*calculate1*arg-num
   (state <s> ^name format-conditions
              ^operator.name calculate1
              ^condition <co>)
   (<co> ^args <a>)
   (<a> -^num ^{<arg> << 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 >>} <p1>)
   (<a> -^{<arg2> << 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 >> > <arg>} <p2>)
-->
   (<a> ^num <arg>)
}

sp {format-conditions*apply*calculate1*parameter-set*argnum
   (state <s> ^name format-conditions
              ^operator.name calculate1
              ^parameter-set <ps>
              ^condition <co>)
   (<ps> -^argnum)
   (<co> ^parameter.{<arg> << 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 >>}.param-id <num>)
  -{(<s> ^condition.parameter.{<arg2> << 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 >>}.param-id > <num>)}
-->
   (<ps> ^argnum <num>)
}

   # elab parameter.1 ^link.set.link.number holder when args.1.rtype = single and return-type = number and parameter.num = 1
   # elab parameter.1 ^link.set.pair.a holder when args.1.rtype = single,set and parameter.num > 1
   # elab parameter.1 ^link.set.item.object holder when args.1.rtype = single and parameter.num = 1 -^return-type number
 
sp {format-conditions*apply*calculate2*parameter-link*set
   (state <s> ^name format-conditions
              ^operator.name calculate2
              ^condition <co>)
   (<co> ^args.<arg>.parameter.result.link.link.<s1>.<i1>.<o1> holder ^parameter.{<arg> 1} <a1> ^parameter.num 1 -^return-type number)# ^args.<arg>.rtype single)
   (<a1> -^link)
-->
   (<a1> ^link.<s1>.<i1>.<o1> holder)
}
sp {format-conditions*apply*calculate2*parameter-link*number
   (state <s> ^name format-conditions
              ^operator.name calculate2
              ^condition <co>)
   (<co> ^parameter.{<arg> 1} <a1> ^parameter.num 1 ^return-type number ^args.<arg>.rtype single)
   (<a1> -^link)
-->
   (<a1> ^link.set.link.number holder)
}
sp {format-conditions*apply*calculate2*parameter-link*pair
   (state <s> ^name format-conditions
              ^operator.name calculate2
              ^{<elem> << a b c >>} <arg>
              ^condition <co>)
   (<co> ^parameter.{<arg> << 1 2 3 >>} <a1> ^parameter.num > 1)
   (<a1> -^link)
-->
   (<a1> ^link.set.pair.<elem> holder)
}
